import "../API/api.thingml"

thing LED_BROWSER includes DRIVER 
{	
	readonly property U : UInt8 = 25 		//number of users in the tutorial
	readonly property T : UInt8 = U * N		//total number of LEDs
	
	property H : UInt16[T] //0..360 degrees
	property S : UInt8[T]  //0..100
	property L : UInt8[T]  //0..100
	
	function ini() do
		`var svg = '';
		let xmax = ` & 30*N & `;
		let ymax = ` & 30*U & `;
		svg += "<svg id=\"logo\" width=" + xmax + " height=" + ymax + " viewBox=\"0 0 " + xmax + " " + ymax + "\">";`
		var j : UInt8 = 0
		while (j < U) do
			var i : UInt8 = 0
			while (i < N) do
				H[j*N + i] = 0
				S[j*N + i] = 0
				L[j*N + i] = 0
				`let id = 'led' + ` & id(j, i) & `;
				let xpos = ''+(30*` & i & `);  
				let ypos = ''+(30*` & j & `);
				svg += "<rect id=" + id + " x=" + xpos + " y=" + ypos + " width=\"25\" height=\"25\" fill=\"#000\" />";`
				i++
			end
			j++
		end	
		`svg += "</svg>"
		document.body.innerHTML += svg;`
	end
	
	function turn_on(id : UInt8) do
		println "Turn on LED", id, " with color ", H[id], ", ", S[id], ", ", L[id]				
		`
		let color = ` & HSL2Hex(H[id], S[id], L[id]) & `; 
		document.getElementById("led"+` & id & `).setAttribute("fill", color);
		`
	end
	
	function turn_off(id : UInt8) do
		println "Turn off LED", id
		`document.getElementById("led"+` & id & `).setAttribute("fill", "#000");`		
	end
	
	function set_color(id : UInt8, h : UInt8, s : UInt8, l : UInt8) do
		println "Set color of LED", id, " to ", h, ", ", s, "%, ", l, "%"
		H[id] = h
		S[id] = s
		L[id] = l	
	end
	
	function HSL2Hex(h : UInt16, s : UInt8, l : UInt8) : String do
		`var h = ` & (h*1.0)/360.0 & `
		var s = ` & (s*1.0)/100.0 & `
		var l = ` & (l*1.0)/100.0 & `
	    var r, g, b;
        if(s == 0){
            r = g = b = l; // achromatic
        }else{
            var hue2rgb = function(p, q, t){
                if(t < 0) t += 1;
                if(t > 1) t -= 1;
                if(t < 1/6) return p + (q - p) * 6 * t;
                if(t < 1/2) return q;
                if(t < 2/3) return p + (q - p) * (2/3 - t) * 6;
                return p;
            }
            var q = l < 0.5 ? l * (1 + s) : l + s - l * s;
            var p = 2 * l - q;
            r = hue2rgb(p, q, h + 1/3);
            g = hue2rgb(p, q, h);
            b = hue2rgb(p, q, h - 1/3);
        }`
        return `'#' + Math.round(r * 255).toString(16) + '' + Math.round(g * 255).toString(16) + '' + Math.round(b * 255).toString(16)`
	end
}