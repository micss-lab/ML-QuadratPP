//Required installation to run this example:
//1) Clone https://github.com/SINTEF-9012/TinyOpenZWaveApi
//2) Navigate to open-zwave/install
//3) Run: make && make install
//4) Navigate to TinyOpenZWaveApi
//5) Run: make && make install
//6) install the udev library, make sure that pthread is also installed

//NOTE:
//make sure that the dynamic linker is set to look up /usr/local/lib
//for more details see http://lonesysadmin.net/2013/02/22/error-while-loading-shared-libraries-cannot-open-shared-object-file/

import "../../thingml.thingml"

object CString
@c_type "const char*";

thing fragment Config {
	property pport : CString = "/dev/ttyUSB0"
	property config : CString = "/usr/local/share/openzwave/config/"
	property zwdir : CString = ""
	property domo_log : CString = "./DomoZWave_Log"
	property enableLog : Boolean = true
	property enableZWLog : Boolean = false
	property polltime : Integer = 0
}


thing fragment ControllerMsg {
	message initialize();
	message initialized();
	message controller_ready();
    message start();
    message started();
  	message node_quiried(nodeid : Integer);
  	message all_nodes_quiried();
}

object TinyController
@c_type "TinyController*";

thing fragment Controller includes ControllerMsg {

	property controller : TinyController

    provided port ctrlport {
    	sends initialized, controller_ready, started, node_quiried, all_nodes_quiried
    	receives initialize, start
    }
    
	statechart behavior init Start {
	
		state Start {
			on entry do
				print "Controller : waiting for command to initialize...\n"
			end
			
			transition->Init
			event ctrlport?initialize
		}
	
		state Init {
			on entry do
				print "Controller : initialize...\n"
				init_controller()
				ctrlport!initialized()
			end
			
			transition->Run
			event ctrlport?start
			
		}
		state Run {
			on entry do
				print "Controller : run...\n"
				start_controller()
				ctrlport!started()
			end
		}
	}
}

thing ZWaveController includes Controller, Config
@c_header "
#include <stdlib.h>
#include <cstdarg>
#include \"tinyozw/TinyZWaveFacade.h\"
#include \"tinyozw/devices/TinyController.h\"
#include \"tinyozw/libs/Utility.h\"

using namespace TinyOpenZWaveApi;
"
{
        
    function controller_ready_callback()
    	@c_prototype "void controller_ready_callback(void *_instance, ...)"
    	@c_instance_var_name "(ZWaveController_Instance *) _instance"
    	do
    		ctrlport!controller_ready()
    	end
    	
    function all_nodes_quiried_callback()
        @c_prototype "void all_nodes_quiried_callback(void *_instance, ...)"
    	@c_instance_var_name "(ZWaveController_Instance *) _instance"
    	do
    		ctrlport!all_nodes_quiried()
    	end
    
    function node_quiries_completed_callback()
     	@c_prototype "void node_quiries_completed_callback(void *_instance, ...)"
    	@c_instance_var_name "(ZWaveController_Instance *) _instance"
    	do
    		'va_list arguments;'
        	'va_start(arguments, _instance);'
        	'int nodeid = va_arg(arguments, int);'
    		'va_end(arguments);'
    		ctrlport!node_quiried('nodeid')
    	end
	
	function init_controller() do
		print "ZWaveNetwork: initializing ... \n"
		'ThingMLCallback* controller_ready = new ThingMLCallback(controller_ready_callback, _instance);'
		'ThingMLCallback* all_nodes_quiried = new ThingMLCallback(all_nodes_quiried_callback, _instance);'
		'ThingMLCallback* node_quiries_complete = new ThingMLCallback(node_quiries_completed_callback, _instance);'
		''& controller &' = OpenZWaveFacade::CreateController('& pport &');'
		''& controller &'->setControllerReadyCallback(controller_ready);'
		''& controller &'->setAllNodeQueriedCallback(all_nodes_quiried);'
		''& controller &'->setNodeQueriesCompleteCallback(node_quiries_complete);'
	end
	
	function start_controller() do
		print "ZWaveNetwork: starting ... \n"
		''& controller &'->start();'
	end
}

thing fragment DoorWindowMsg{
	message init_dw();
	message dw_init();
	message dw_closed();
	message dw_opened();
	message start_monitoring();
	message ready_to_monitor();
	
	//internal messages
	message dw_changed(dwstate : Integer);
	message dw_ready();
}

object BinarySensor
@c_type "BinarySensor*";

thing fragment DoorWindow includes DoorWindowMsg {
	property nodeId : Integer
	property cInstance : Integer
	property cIndex : Integer
    property bs : BinarySensor
    property dwstate : Integer
    
    provided port doorwindowport {
    	sends dw_closed, dw_opened, dw_init, ready_to_monitor
    	receives init_dw, start_monitoring
    }
    
    provided port dwintsendport {
    	sends dw_changed, dw_ready
    }
    
    required port dwintrecport {
    	receives dw_changed, dw_ready
    }
    
    statechart behavior init Start {
    
    	state Start {
    		on entry do
				print "DoorWindow: starting...\n"
			end
    		transition->Init
    		event doorwindowport?init_dw
    	}
    	
    	state Init {
			on entry do 
				print "DoorWindow: initializing...\n"
				init_doorwindow()
				doorwindowport!dw_init()
            end
            
            transition -> Ready
            event dwintrecport?dw_ready
            action do
                dwstate = getState()
            end
		}
				
		state Ready {
			on entry do
				print "DoorWindow: ready ...\n"
				doorwindowport!ready_to_monitor()
			end
			
			transition->Closed
			event doorwindowport?start_monitoring
			guard dwstate == 0
			
			transition->Opened
			event doorwindowport?start_monitoring
			guard dwstate == 1
		}
		
		state Closed {
			on entry do
				print "DoorWindow: closed...\n"
			end
			
			transition->Opened
			event e : dwintrecport?dw_changed
			guard e.dwstate == 0
			action do
				doorwindowport!dw_opened()
			end
		}
		
		state Opened {
			on entry do
				print "DoorWindow: opened...\n"
			end
			
			transition->Closed
			event e : dwintrecport?dw_changed
			guard e.dwstate == 1
			action do
				doorwindowport!dw_closed()
			end
		}
    }
}

thing FibaroDoorWindow includes DoorWindow, Config
@c_header "
#include <stdlib.h>
#include <cstdarg>
#include \"tinyozw/TinyZWaveFacade.h\"
#include \"tinyozw/devices/BinarySensor.h\"
#include \"tinyozw/libs/Utility.h\"

using namespace TinyOpenZWaveApi;
"
{	
	function init_binarysensor_callback()
    @c_prototype "void init_binarysensor_callback(void *_instance, ...)"
    @c_instance_var_name "(FibaroDoorWindow_Instance *) _instance"
    do
    	dwintsendport!dw_ready()
    end
    
    function value_change_binarysensor_callback()
    @c_prototype "void value_change_binarysensor_callback(void *_instance, ...)"
    @c_instance_var_name "(FibaroDoorWindow_Instance *) _instance"
    do
		'va_list arguments;'
        'va_start(arguments, _instance);'
        'int state = va_arg(arguments, int);'
    	'va_end(arguments);'
    	dwintsendport!dw_changed('state')
    end
	
	function init_doorwindow() do
        print "FibaroDoorWindow: initializing ... \n"
        'ThingMLCallback* value_changed = new ThingMLCallback(value_change_binarysensor_callback, _instance);'
        'ThingMLCallback* device_init = new ThingMLCallback(init_binarysensor_callback, _instance);'
        bs = 'new BinarySensor();'
        bs = ''&bs&'->BinarySensor::Init(OpenZWaveFacade::GetController(' & pport & '), (uint8)' & nodeId & ',(uint8)' & cInstance & ', (uint8)'& cIndex &');'
        ''&bs&'->setDeviceInitCallback(device_init);'
		''&bs&'->setValueUpdatedCallback(value_changed);'
	end

	function getState() : Integer do
		return ''&bs&'->getCurrentValue()'
	end
}


thing fragment PlugMsg {
	message turn_on();
	message turn_off();
	message turned_off();
	message turned_on();
	message no_change();
	message plug_init();
    message plug_ready();
    message plug_exit();
    message init_plug();
    message switch_ready();
    message switch_state( swstate : Boolean);
}

object BinarySwitch
@c_type "BinarySwitch*";

thing fragment Plug includes PlugMsg {

	property nodeId : Integer
	property cInstance : Integer
	property cIndex : Integer
    property bs : BinarySwitch
    property attempts : Integer = 0
    property exit_after_tries : Integer = 0
    property MAX_ATTEMPTS : Integer = 3
    property MAX_EXIT_AFTER :Integer = 20
	
	provided port switchport {
		receives turn_on, turn_off
	}
	
	provided port plugport {
		sends plug_init, plug_exit
		receives init_plug
	}

    provided port swintsendport {
        sends switch_state, switch_ready, turned_off, no_change, turned_on
    }

    required port swintrecport {
        receives switch_ready, switch_state, turned_off, no_change, turned_on
    }
	
	statechart behavior init Start {
		
		state Start {
			on entry do
				print "Plug: starting...\n"
			end
			
			transition -> Ready
			event plugport?init_plug
		}
		
		state Ready {
			on entry do 
				print "Plug: initializing...\n"
				init_plug()
				plugport!plug_init()
            end
            
            transition -> TurnedOnOROff
            event swintrecport?switch_ready
		}
		
		state TurnedOnOROff {
			on entry do
				print "Plug: finding out plug state...\n"
				swintsendport!switch_state(isOn())
			end
			
			transition->TurnedOn
			event e : swintrecport?switch_state
			guard e.swstate
			
			transition->TurnedOff
			event e : swintrecport?switch_state
			guard not e.swstate
		} 
		
		state TurnedOn {
			on entry do
				print "Plug: is on...\n"
			end
			
			transition->TurningOff
			event switchport?turn_off
		}
		
		state TurningOff {
			on entry do
				print "Plug: attempting to turn off the plug...\n"
				turn_off()
				attempts = attempts + 1
			end
			
			transition->TurnedOff
			event swintrecport?turned_off
            guard exit_after_tries < MAX_EXIT_AFTER or exit_after_tries == MAX_EXIT_AFTER
            action do
                exit_after_tries = exit_after_tries + 1
            end
            
            transition->Exit
			event swintrecport?turned_off
            guard exit_after_tries > MAX_EXIT_AFTER
			
			transition->TurningOff
			event swintrecport?no_change
			guard attempts < MAX_ATTEMPTS
			
			transition->TurnedOnOROff
			event swintrecport?no_change
			guard attempts == MAX_ATTEMPTS or attempts > MAX_ATTEMPTS
			action attempts = 0
		}
		
		state TurnedOff {
			on entry do
				print "Plug: is off...\n"
			end
			
			transition->TurningOn
			event switchport?turn_on
		}
		
		state TurningOn {
			on entry do
				print "Plug: attempting to turn on the plug...\n"
				turn_on()
				attempts = attempts + 1
			end
			
			transition->TurnedOn
			event swintrecport?turned_on
            guard exit_after_tries < MAX_EXIT_AFTER or exit_after_tries == MAX_EXIT_AFTER
            action do
                exit_after_tries = exit_after_tries + 1
            end
            
            transition->Exit
			event swintrecport?turned_on
            guard exit_after_tries > MAX_EXIT_AFTER
            			
			transition->TurningOn
			event swintrecport?no_change
			guard attempts < MAX_ATTEMPTS
			
			transition->TurnedOnOROff
			event swintrecport?no_change
			guard attempts == MAX_ATTEMPTS or attempts > MAX_ATTEMPTS
			action attempts = 0
		}

        state Exit {
            on entry do
                print "Plug : exiting...\n"
                plugport!plug_exit()
            end
        }
	}
}

thing FibaroPlug includes Plug, Config
@c_header "
#include \"tinyozw/TinyZWaveFacade.h\"
#include \"tinyozw/devices/BinarySwitch.h\"
#include \"tinyozw/libs/Utility.h\"

using namespace TinyOpenZWaveApi;
"
 {
    
    function turned_on_callback()
    @c_prototype "void turned_on_callback(void *_instance, ...)"
    @c_instance_var_name "(FibaroPlug_Instance *) _instance"
    do
        swintsendport!turned_on()
    end
    
    function turned_off_callback()
    @c_prototype "void turned_off_callback(void *_instance, ...)"
    @c_instance_var_name "(FibaroPlug_Instance *) _instance"
    do
        swintsendport!turned_off()
    end
    
    function no_change_callback()
    @c_prototype "void no_change_callback(void *_instance, ...)"
    @c_instance_var_name "(FibaroPlug_Instance *) _instance"
    do
        swintsendport!no_change()
    end
    
    function init_callback()
    @c_prototype "void init_callback(void *_instance, ...)"
    @c_instance_var_name "(FibaroPlug_Instance *) _instance"
    do
    	swintsendport!switch_ready()
    end
	
	function init_plug() do
        print "FibaroPlug: initializing ... \n"
        'ThingMLCallback* turned_on = new ThingMLCallback(turned_on_callback, _instance);'
        'ThingMLCallback* turned_off = new ThingMLCallback(turned_off_callback, _instance);'
        'ThingMLCallback* no_change = new ThingMLCallback(no_change_callback, _instance);'
        'ThingMLCallback* device_init = new ThingMLCallback(init_callback, _instance);'
        bs = 'new BinarySwitch();'
        bs = ''&bs&'->BinarySwitch::Init(OpenZWaveFacade::GetController(' & pport & '), (uint8)' & nodeId & ',(uint8)' & cInstance & ', (uint8)'& cIndex &');'
        ''&bs&'->setDeviceInitCallback(device_init);'
		''&bs&'->setTurnedOnCallback(turned_on);'
		''&bs&'->setTurnedOffCallback(turned_off);'
		''&bs&'->setNoChangeCallback(no_change);'
	end
	
    function turn_on() do
         print "FibaroPlug: turning plug on ... \n"
        ''&bs&'->turnOn();'
    end

    function turn_off() do
        print "FibaroPlug: turning plug off ... \n"
        ''&bs&'->turnOff();'
    end
    
    function isOn() : Boolean do
    	return ''&bs&'->isOn();'
    end
}

thing fragment HomeMsg {
	message devicesinitialized();
	message devicesquiried();
}

thing fragment HomeCenter includes PlugMsg, ControllerMsg, DoorWindowMsg, HomeMsg, Config {
	
	property plugid : Integer
	property doorwindowid : Integer
	
	property plugIsInitialized : Boolean = false
	property doorwindowInitialized : Boolean = false
	
	property plugIsQuiried : Boolean = false
	property doorwindowIsQuiried : Boolean = false
	
	provided port homeintsendport {
		sends devicesinitialized, devicesquiried
	}
	
	required port homeintrecport {
		receives devicesinitialized, devicesquiried
	}
	
	required port switchport {
		sends turn_on, turn_off
		receives turned_off, turned_on, no_change
	}
	
	required port plugport {
		sends init_plug
		receives plug_init, plug_exit
	}
	
	required port ctrlport {
		sends initialize, start
		receives initialized, started, node_quiried, all_nodes_quiried, controller_ready
	}
		
	required port doorwindowport {
		sends init_dw, start_monitoring
		receives dw_init, dw_closed, dw_opened, ready_to_monitor
	}

	statechart behavior init Start {
		state Start {
			on entry do
				print "HomeCenter: initialize network ...\n"
				init_network()
				ctrlport!initialize()
			end
			
			transition -> InitDevice
			event e : ctrlport?initialized
		}
		
		state InitDevice {
			on entry do
				print "HomeCenter: initialize home device ...\n"
				plugport!init_plug()
				doorwindowport!init_dw()
			end
			
			internal event plugport?plug_init
			action do
				print "HomeCenter: plug is ready \n"
				plugIsInitialized = true
				if(plugIsInitialized and doorwindowInitialized) do
					homeintsendport!devicesinitialized()
				end
			end
			
			internal event doorwindowport?dw_init
			action do
				print "HomeCenter: doorwindow is ready \n"
				doorwindowInitialized = true
				if(plugIsInitialized and doorwindowInitialized) do
					homeintsendport!devicesinitialized()
				end
			end
			
			transition->StartController
			event homeintrecport?devicesinitialized
		}
		
		state StartController {
			on entry do
				print "HomeCenter: start controller ...\n"
				ctrlport!start()
			end
			
			transition->WaitForDevices
			event ctrlport?controller_ready
		}
		
		state WaitForDevices {
			on entry do
				print "HomeCenter: waiting for device to be quiried ...\n"
			end
			
			internal event e : ctrlport?node_quiried
			guard e.nodeid == plugid
			action do
				print "HomeCenter: plug is quiried ...\n"
				plugIsQuiried = true
				if(plugIsQuiried and doorwindowIsQuiried) do
					homeintsendport!devicesquiried()
				end
			end
			
			internal event e : ctrlport?node_quiried
			guard e.nodeid == doorwindowid
			action do
				print "HomeCenter: doorwindow is quiried ...\n"
				doorwindowIsQuiried = true
				if(plugIsQuiried and doorwindowIsQuiried) do
					homeintsendport!devicesquiried()
				end
			end
			
			transition->DevicesAreReady
			event homeintrecport?devicesquiried
		}
		
		state DevicesAreReady {
			on entry do
				print "HomeCenter: all devices are ready to use"
				doorwindowport!start_monitoring()
			end
		
			internal event doorwindowport?dw_closed
			action do
				switchport!turn_on()
				switchport!turn_off()
			end
			
			internal event doorwindowport?dw_opened
			action do
				switchport!turn_on()
				switchport!turn_off()
			end

            transition->Quit
            event plugport?plug_exit
		}

        state Quit {
            on entry do
				print "HomeCenter: Quit...\n"
                quite_network()
			end
        }
	}
}

thing ZWaveHomeCenter includes HomeCenter 
@c_header "
#include \"tinyozw/TinyZWaveFacade.h\"
#include \"tinyozw/libs/Utility.h\"

using namespace TinyOpenZWaveApi;
"
{
	
	function init_network()
		do
			'OpenZWaveFacade::Init('& config &','& zwdir &','& domo_log &','& enableLog &','& enableZWLog &','& polltime &');'
		end
	
	function quite_network()
		do
			'OpenZWaveFacade::Quite();'
		end
}

configuration LinuxHomeCenterBSwitchBSensor
  @output_folder "/home/tmp/"
  @debug "true"
  @debug_fifo "true"
  @debug_message_send ".*"
  @debug_message_receive ".*"
  @add_c_libraries "udev pthread openzwave tinyozw"
{
	instance p1 : FibaroPlug
	set p1.nodeId = 2
	set p1.cInstance = 1
	set p1.cIndex = 0
	
	instance dw : FibaroDoorWindow
	set dw.nodeId = 3
	set dw.cInstance = 1
	set dw.cIndex = 0
	
	instance app : ZWaveHomeCenter
	set app.plugid = 2
	set app.doorwindowid = 3
		
	instance n : ZWaveController

	// Create and connect the test app
	connector app.switchport => p1.switchport
	connector app.ctrlport => n.ctrlport
    connector app.plugport => p1.plugport
    connector app.doorwindowport => dw.doorwindowport

    connector p1.swintrecport => p1.swintsendport
    connector dw.dwintrecport => dw.dwintsendport
    connector app.homeintrecport => app.homeintsendport
}
