/* ThingML Framework */

import "../datatypes.thingml"

thing fragment InputByteStreamMsgs {
	message receive_byte (b : Byte);
}

thing fragment OutputByteStreamMsgs {
	message write_byte (b : Byte);
}

thing fragment InputByteArrayMsgs {
	message receive_bytes(b : Byte[256]);
}

thing fragment OutputByteArrayMsgs {
	message write_bytes(b : Byte[256]);
}

thing fragment FormatedOutputStreamMsgs {
	message print_message (msg : String);
	message print_value (v : Int16);
}

thing fragment IOStream includes InputByteStreamMsgs, OutputByteStreamMsgs
{
	provided port IOStream 
    {
		sends receive_byte
        receives write_byte
	}
}

thing fragment IOByteArray includes InputByteArrayMsgs, OutputByteArrayMsgs
{
	provided port IOStream 
    {
		sends receive_bytes
        receives write_bytes
	}
}

//Just a simple mock to test serialization without using real comm channel.
thing Network includes IOStream
{
    statechart behavior init default {
        state default {
            internal event wb : IOStream?write_byte
            action do
                print("network forwards: " + wb.b)
                IOStream!receive_byte(wb.b)
            end
        }    
    }    
}

//Just a simple mock to test serialization without using real comm channel.
thing NetworkArray includes InputByteArrayMsgs, OutputByteArrayMsgs
{

	required port in
    {
		receives receive_bytes
	}

    required port out 
    {
        sends write_bytes
    }

    statechart behavior init default {
        state default {            
            internal event wb : in?receive_bytes
            action do
                print("network forwards: " + wb.b)
                out!write_bytes(wb.b)
            end
        }    
    }    
}

thing Serial includes IOStream, FormatedOutputStreamMsgs
{
	provided port Serial
	{
		receives print_message, print_value
	}
}
