//*
// Fabien Fleurey - 22/08/2011
// Electronic bricks library.
//
// This file provides an interface with the water sensor. 
//*

import "../../thingml.thingml"
import "../io/digital_input.thingml"

thing fragment WaterSensorMsgs 
{
	message wet ();
	message dry ();
}

thing WaterSensorClient includes WaterSensorMsgs 
{
	required port WaterSensor 
    {
		receives wet, dry
	}    
}

thing WaterSensor includes WaterSensorMsgs, DigitalInputMsgs
{  
	provided port WaterSensor 
    {
		sends wet, dry
	}

    required port DigitalInput
    {
        receives digital_input_value
        sends read_digital_input    
    }
    
    statechart WaterSensorImpl init Running 
    {        
        state Running 
        {   
            internal event m : DigitalInput?digital_input_value
            guard m.value == DigitalState:HIGH
    	    action WaterSensor!dry ()
    
            internal event m : DigitalInput?digital_input_value
            guard m.value == DigitalState:LOW
    	    action WaterSensor!wet ()
        }   
    }
}