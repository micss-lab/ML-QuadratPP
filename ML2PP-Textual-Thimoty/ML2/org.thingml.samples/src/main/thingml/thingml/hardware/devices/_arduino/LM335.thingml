import "../../arduino/_arduino/arduino.thingml"
import "../../io/_arduino/analog_input.thingml"


thing fragment LM335Msg {
    message temperature(temperature : Int16);    
}

thing LM335 includes AnalogInputMsgs, LM335Msg
{
    
    provided port Temp {
        sends temperature    
    }
            
    required port AnalogInput
    {
        receives analog_input_value
        sends read_analog_input    
    }
	
	statechart LM335Impl init Init
	{
    
        property temp : Int16
        
		on entry AnalogInput!read_analog_input()

        state Init {
            transition initialize -> Running
			event m : AnalogInput?analog_input_value
			action do
                // Initialize the temp variable
                temp =  m.value * '0.488' - 273
            end
        }
		
		state Running
		{
			transition refresh -> Running
			event m : AnalogInput?analog_input_value
			action do
                // Compute temperture by averaging with the previous value
                temp = temp + m.value * '0.488' - 273
                temp = (temp + 1) / 2
                Temp!temperature(temp)
            end
		}
	}
}

configuration fragment LM335
{
   group io : PeriodicAnalogInputArduino
       
   instance lm335 : LM335
   connector  lm335.AnalogInput => io.analog_input.AnalogInput
   
   set io.periodic_analog_input.refresh_rate = 500 // 500 ms sampling rate
   set io.analog_input.pin = AnalogPin:A_4 // Should be reset when using this fragment
}