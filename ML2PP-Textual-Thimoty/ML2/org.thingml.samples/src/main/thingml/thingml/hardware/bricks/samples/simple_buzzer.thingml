//*
// Fabien Fleurey - 20/08/2011
//
// This is an example of ThingML use to generate Arduino code which uses 
// Electronic Brick components.
//  
// Requirements:
//      - buzzer (1)    ->  Default pin: D_8
//
// The buzzer plays an SOS signal.
//*

import "../../bricks/buzzer.thingml"

thing SimpleBuzzer includes BuzzerMsgs, TimerMsgs
{
	property counter : UInt8 = 0
    property end_motif : Boolean = false 
	
	required port Buzzer
	{
		sends buzzer_delay
	}
	
	required port Timer
	{
		sends timer_start
		receives timer_timeout
	}
	
	statechart SimpleBuzzerImpl init Wait
	{       
		state Fast
		{
            on entry counter = 0
            
			internal repeat event Timer?timer_timeout
            guard counter < 3
            action do
                Buzzer!buzzer_delay (100)
                counter = counter + 1
                Timer!timer_start (150)
                end
                
            transition switch -> Slow
            event Timer?timer_timeout 
            guard counter == 3 and not end_motif
            action Timer!timer_start (50)
            
            transition waiting -> Wait
            event Timer?timer_timeout
            guard counter == 3 and end_motif
		}
			
		state Slow
		{	
            on entry counter = 0
            on exit end_motif = true
                
            internal repeat event Timer?timer_timeout
            guard counter < 3
            action do
                Buzzer!buzzer_delay (200)
                counter = counter + 1
                Timer!timer_start (250)
                end
                
            transition switch -> Fast
            event Timer?timer_timeout
            guard counter == 3
            action Timer!timer_start (50)
		}

        state Wait
        {
            on entry Timer!timer_start (1000)
            on exit Timer!timer_start (50)
            
            transition begin -> Fast
            event Timer?timer_timeout
            action end_motif = false
        } 
	}
}
