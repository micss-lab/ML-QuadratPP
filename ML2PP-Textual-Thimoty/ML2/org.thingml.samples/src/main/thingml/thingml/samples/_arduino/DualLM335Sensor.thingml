import "../../hardware/bricks/_arduino/led.thingml"
import "../../hardware/bricks/_arduino/lcd_screen.thingml"
import "../../hardware/io/_arduino/analog_input.thingml"

thing DualLM335 includes LcdScreenMsgs, AnalogInputMsgs
{
    
    required port LcdScreen
	{
		sends print_string, set_cursor, print_value, clear
	}
    
    required port Sensor1
    {
        receives analog_input_value 
    }
    
     required port Sensor2
    {
        receives analog_input_value 
    }
	
	statechart SimpleRotSensorDisplayImpl init Running
	{
    
        property temp1 : Int16
        property temp2 : Int16
        
		
		state Running
		{
			transition refresh -> Running
			event m : Sensor1?analog_input_value
			action do
                // Compute temperture
                temp1 = temp1 + m.value * '0.488' - 273
                temp1 = (temp1 + 1) / 2
                // Update Display
                LcdScreen!clear()
                LcdScreen!print_string ("  IN = ")
                LcdScreen!print_value (temp1)
                LcdScreen!print_string (" C")
                LcdScreen!set_cursor (0, 1)
                LcdScreen!print_string (" OUT = ")
                LcdScreen!print_value (temp2)
                LcdScreen!print_string (" C")
            end
            
            transition refresh -> Running
			event m : Sensor2?analog_input_value
			action do 
                temp2 = temp2 + m.value * '0.488' - 273
                temp2 = (temp2 + 1) / 2
            end
		}
	}
}



configuration DualLM335
{
   group io1 : PeriodicAnalogInputArduino
   set io1.analog_input.pin = AnalogPin:A_4
   set io1.periodic_analog_input.refresh_rate = 500
   
   group io2 : PeriodicAnalogInputArduino
   set io2.analog_input.pin = AnalogPin:A_5
   set io2.periodic_analog_input.refresh_rate = 500
   
   instance lcd_screen : LcdScreen
       
   instance main : DualLM335
   connector main.Sensor1 => io1.analog_input.AnalogInput
   connector main.Sensor2 => io2.analog_input.AnalogInput
   connector main.LcdScreen => lcd_screen.LcdScreen
}