import "../../../../datatypes.thingml"

thing fragment PingPongMsgs {
	message ping(u : UInt8) @code "16";
	message pong(u : UInt8) @code "165";
}


thing PingServer includes PingPongMsgs {

	provided port ping_service {
		sends pong
		receives ping
	}

	statechart PongServerMachine init Active {

        on entry print "Ping Server Started!\n"

		state Active {
			
			internal event e : ping_service?ping
			action do
				print "ping: "
				print e.u
				print "\n"
				ping_service!pong(e.u + 50)
			end
			
		}
	}
}


protocol UDP
@udp_local_port "1050"
@udp_remote_port "1051"
@udp_address "127.0.0.1"
@serializer "JSON"
;

configuration UDPSrv 
{

	instance server: PingServer

	connector server.ping_service over UDP

}
