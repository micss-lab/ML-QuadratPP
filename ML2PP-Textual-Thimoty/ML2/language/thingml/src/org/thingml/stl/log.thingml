import "datatypes.thingml" from stl

thing fragment LogMsgs {
	
	message log_on()
	message log_off()
	
	/*
	 * inst: name of the thing instance as defined in the configuration
	 * fn_name: name of the invoked function
	 * ty: return type of the function ("void" for no return type)
	 * returns: value returned by the function ("" for no return)
	 * params: list of parameters ("" for no param, "p1=v1, p2=v2, ...pn=vn")
	 */
	message function_called(inst : String, fn_name : String, ty : String, returns : String, params : String)
	
	/*
	 * inst: name of the thing instance as defined in the configuration
	 * prop_name: name of the property that has been updated
	 * ty: type of the property
	 * old: value before update (serialized as string)
	 * new: value after update (serialized as string) 
	 */
	message property_changed(inst : String, prop_name : String, ty : String, old_value : String, new_value : String)
	
	message message_sent(inst : String, port_name : String, msg_name : String, params : String)
	message message_lost(inst : String, port_name : String, msg_name : String, params : String)
	message message_handled(inst : String, source : String, target : String, port_name : String, msg_name : String, params : String)
}

/*
 * Things we want to log needs to include WithLog 
 */
thing fragment WithLog includes LogMsgs {
	
	readonly property DEBUG_ID : String
	
	required port log @sync_send "true" {
		sends function_called, property_changed, message_lost, message_handled, message_sent
	}
	
}

/*
 * Generic proxy listening to things WithLog and forwarding to 
 */
thing fragment Logger includes LogMsgs {
	
	readonly property ACTIVATE_ON_STARTUP : Boolean
	
	provided port log {
		receives function_called, property_changed, message_lost, message_handled, message_sent, log_on, log_off
	}
	
	abstract function log_function_called(inst : String, fn_name : String, ty : String, returns : String, params : String)
	abstract function log_property_changed(inst : String, prop_name : String, ty : String, old_value : String, new_value : String)
	abstract function log_message_lost(inst : String, port_name : String, msg_name : String, params : String)
	abstract function log_message_sent(inst : String, port_name : String, msg_name : String, params : String)
	abstract function log_message_handled(inst : String, source : String, target : String, port_name : String, msg_name : String, params : String)
		
	statechart init STARTUP {
		
		state STARTUP {
			
			transition -> ON
			guard ACTIVATE_ON_STARTUP
			
			transition -> OFF
			guard not ACTIVATE_ON_STARTUP
			
		}
		
		state ON {
			
			internal event d : log?function_called
			action log_function_called(d.inst, d.fn_name, d.ty, d.returns, d.params)
			
			internal event d : log?property_changed
			action log_property_changed(d.inst, d.prop_name, d.ty, d.old_value, d.new_value)
			
			internal event d : log?message_lost
			action log_message_lost(d.inst, d.port_name, d.msg_name, d.params)
			
			internal event d : log?message_sent
			action log_message_sent(d.inst, d.port_name, d.msg_name, d.params)
			
			internal event d : log?message_handled
			action log_message_handled(d.inst, d.source, d.target, d.port_name, d.msg_name, d.params)
			
			transition -> OFF
			event log?log_off
		}
		
		state OFF {
			transition -> ON
			event log?log_on
		}
		
	}
	
}

thing ConsoleLogger includes Logger @stdout_sync "true" {
	
	function log_function_called(inst : String, fn_name : String, ty : String, returns : String, params : String)
		println "log: function_called(", inst, ", ", fn_name, ", ", ty, ", ", returns, ", ", params, ")"
		
	function log_property_changed(inst : String, prop_name : String, ty : String, old_value : String, new_value : String)
		println "log: property_changed(", inst, ", ", prop_name, ", ", ty, ", ", old_value, ", ", new_value, ")"
		
	function log_message_lost(inst : String, port_name : String, msg_name : String, params : String)
		println "log: message_lost(", inst, ", ", port_name, ", ", msg_name, ", ", params, ")"
	
	function log_message_sent(inst : String, port_name : String, msg_name : String, params : String)
		println "log: message_sent(", inst, ", ", port_name, ", ", msg_name, ", ", params, ")"
		
	function log_message_handled(inst : String, source : String, target : String, port_name : String, msg_name : String, params : String)		
		println "log: message_handled(", inst, ", ", source, ", ", target, ", ", port_name, ", ", msg_name, ", ", params, ")"
}